unit LoginSSO.Logic;

interface

uses
  System.SysUtils, System.Classes, IdHTTPServer, IdCustomHTTPServer, IdContext,
  System.Net.HttpClient, System.Net.URLClient, System.Net.HttpClientComponent
 , LoginSSO.User.Information, System.JSON;

type
  TMicrosoftSSO = class
  private
    { Private declarations }
    FHTTPServer: TIdHTTPServer;
    FClientID: string;
    FClientSecret: string;
    FTenantID: string;
    FRedirectURI: string;
    FAuthorizationCode: string;
    FAccessToken: string;
    FLoggedIn: boolean;
    FAuthURL: string;


    procedure HTTPServerCommandGet(AContext: TIdContext; ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo);
    procedure RedirectToAuthorizationEndpoint;
    function ExecuteRequest(const AURL: string; const AParams: string; const AMethod: string; const AHeaders: TNetHeaders): IHTTPResponse;
    function ResponseContentUserData: IHTTPResponse;
    function ResponseContentAccessToken: IHTTPResponse;
    function ParseJsonResponseUserData(AResponseContent: string): TSSOUserInformationModel;
    function ParseJsonResponseAccessToken(AResponseContent: string): string;

    function CreateHTTPComponents(out AHTTPClient: TNetHTTPClient; out AHTTPRequest: TNetHTTPRequest): TNetHTTPRequest;
    function ExecuteHTTPRequest(AHTTPRequest: TNetHTTPRequest; const AURL: string; const AParams: TStringStream; const AMethod: string; const AHeaders: TNetHeaders): IHTTPResponse;

    function ParseJsonErrorAccessToken(AResponse: string): string;
    procedure ParseJsonErrorUserData(AResponse: string);

    function GetEncoding: TEncoding;

  public
    { Public declarations }
    constructor Create(AClientID, ATenantID, ARedirectURI: string);
    destructor Destroy; override;
    procedure StartServer;
    procedure StopServer;
    function GetAccessToken: string;
    function GetAuthCode: string;
    procedure LogOut;
    function GetUserData: TSSOUserInformationModel;

    function GetAuthURL: string;

    function LogIn: TSSOUserInformationModel;

    property LoggedIn : boolean read FLoggedIn write FLoggedIn;
  end;

implementation

uses
 Winapi.ShellAPI, Vcl.Dialogs, System.NetEncoding, Winapi.Windows
 , System.IOUtils, LoginSSO.Constants, LoginSSO.Messages, System.Generics.Collections;

 { TMicrosoftSSO }

constructor TMicrosoftSSO.Create(AClientID, ATenantID, ARedirectURI: string);
begin

  FClientID := AClientID;
  FTenantID := ATenantID;
  FRedirectURI := ARedirectURI;

  FHTTPServer := TIdHTTPServer.Create(nil);
  FHTTPServer.DefaultPort := DEFAULT_PORT;
  FHTTPServer.OnCommandGet := HTTPServerCommandGet;

  LoggedIn := false;
end;

destructor TMicrosoftSSO.Destroy;
begin
  FHTTPServer.Free;
  inherited;
end;

procedure TMicrosoftSSO.StartServer;
begin
  FHTTPServer.Active := True;
end;

procedure TMicrosoftSSO.StopServer;
begin
  FHTTPServer.Active := False;
end;

function TMicrosoftSSO.GetAuthCode: string;
begin
  if FAuthorizationCode <> EmptyStr then
  begin
    Result := FAuthorizationCode;
  end
  else
  begin
    Result := EmptyStr;
  end;

end;


function TMicrosoftSSO.GetAuthURL: string;
begin
  FAuthURL := Format(
    DEFAULT_MICROSOFTURL + AUTH_URL,
    [FTenantID, FClientID, TNetEncoding.URL.Encode(FRedirectURI)]);

  Result := FAuthURL;
end;

function TMicrosoftSSO.GetEncoding: TEncoding;
begin
  Result := TEncoding.UTF8;
end;

procedure TMicrosoftSSO.HTTPServerCommandGet(AContext: TIdContext; ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo);
begin
  if ARequestInfo.Document = CALLBACK then
  begin
    FAuthorizationCode := ARequestInfo.Params.Values[CODE];
    AResponseInfo.ContentText := CALLBACK_HTML_PAGE;
  end
  else
  begin
    AResponseInfo.ResponseNo := HTTP_STATUS_NOT_FOUND;
    AResponseInfo.ContentText := NOT_FOUND;
  end;
end;


procedure TMicrosoftSSO.RedirectToAuthorizationEndpoint;
begin
    ShellExecute(0, OPEN, PChar(GetAuthURL), nil, nil, SW_SHOWNORMAL);
end;


function TMicrosoftSSO.ResponseContentUserData: IHTTPResponse;
var
  LHeader: TNetHeaders;
begin
  SetLength(LHeader, 3);
  LHeader[AUTHORIZATION_NUM].Name := AUTHORIZATION;
  LHeader[AUTHORIZATION_NUM].Value := BEARER + FAccessToken;
  LHeader[ACCEPT_NUM].Name := ACCEPT;
  LHeader[ACCEPT_NUM].Value := APPLICATION_JSON;
  LHeader[USER_AGENT_NUM].Name := USER_AGENT;
  LHeader[USER_AGENT_NUM].Value := MOZILLA_CONST;

  Result := ExecuteRequest(MICROSOFT_GRAPH_URL, EmptyStr, GET, LHeader);
end;

function TMicrosoftSSO.ResponseContentAccessToken: IHTTPResponse;
var
  LURL: string;
  LPostData: string;
begin

  LURL := DEFAULT_MICROSOFTURL + FTenantID + TOKEN_URL;
  LPostData := Format(
    TOKEN_BODY_URL,
    [TNetEncoding.URL.Encode(FClientID),
     TNetEncoding.URL.Encode(FClientSecret),
     TNetEncoding.URL.Encode(FRedirectURI),
     TNetEncoding.URL.Encode(FAuthorizationCode)]
  );

  Result := ExecuteRequest(LURL, LPostData, POST, []);
end;

function TMicrosoftSSO.ParseJsonResponseUserData(AResponseContent: string): TSSOUserInformationModel;
var
  LJSONObj: TJSONObject;
  LJSONArray: TJSONArray;
  I: Integer;
begin
  // Analizza la risposta JSON
  LJSONObj := TJSONObject.ParseJSONValue(AResponseContent) as TJSONObject;
  try
    if not Assigned(LJSONObj) then
      raise Exception.Create(JSON_PARSE_ERROR);

    Result := TSSOUserInformationModel.Create;

    // Salvo i valori che mi interessano dal oggetto JSON
    Result.DisplayName := LJSONObj.GetValue<string>(DISPLAYNAME);
    Result.GivenName := LJSONObj.GetValue<string>(GIVENNAME);
    Result.JobTitle := LJSONObj.GetValue<string>(JOBTITLE);
    Result.EMail := LJSONObj.GetValue<string>(EMAIL);
    Result.MobilePhone := LJSONObj.GetValue<string>(MOBILEPHONE);
    Result.OfficeLocation := LJSONObj.GetValue<string>(OFFICELOCATION);
    Result.PreferredLanguage := LJSONObj.GetValue<string>(PREFERREDLANGUAGE);
    Result.Surname := LJSONObj.GetValue<string>(SURNAME);
    Result.UserPrincipalName := LJSONObj.GetValue<string>(USERPRINCIPALNAME);
    Result.Id := LJSONObj.GetValue<string>(ID);


    LJSONArray := LJSONObj.GetValue<TJSONArray>(BUSINESSPHONES);

    if Assigned(LJSONArray) then
    begin
      for I := 0 to LJSONArray.Count - 1 do
      begin
        Result.BusinessPhones.Add(LJSONArray.Items[I].Value);
      end;
    end;

  finally
    LJSONObj.Free;
  end;
end;

function TMicrosoftSSO.ParseJsonResponseAccessToken(AResponseContent: string): string;
var
  LJSONObj: TJSONObject;
begin
  LJSONObj := TJSONObject.ParseJSONValue(AResponseContent) as TJSONObject;
  try
    if  not Assigned(LJSONObj) then
      raise Exception.Create(JSON_PARSE_ERROR);

    //salvo il valore dell access token in una variabile private della classe
    Result := LJSONObj.GetValue<string>(ACCESS_TOKEN);

  finally
    LJSONObj.Free;
  end;
end;



function TMicrosoftSSO.CreateHTTPComponents(out AHTTPClient: TNetHTTPClient; out AHTTPRequest: TNetHTTPRequest): TNetHTTPRequest;
begin
  AHTTPClient := TNetHTTPClient.Create(nil);
  AHTTPRequest := TNetHTTPRequest.Create(nil);
  AHTTPRequest.Client := AHTTPClient;
  Result := AHTTPRequest;
end;

function TMicrosoftSSO.ExecuteHTTPRequest(AHTTPRequest: TNetHTTPRequest; const AURL: string; const AParams: TStringStream; const AMethod: string; const AHeaders: TNetHeaders): IHTTPResponse;
begin
  if AMethod = POST then
  begin
    Result := AHTTPRequest.Post(AURL, AParams, nil, AHeaders);
  end
  else if AMethod = GET then
  begin
    Result := AHTTPRequest.Get(AURL, nil, AHeaders);
  end
  else
  begin
    raise Exception.Create(Format(UNSUPPORTED_METHOD_ERROR, [AMethod]));
  end;
end;



function TMicrosoftSSO.ExecuteRequest(const AURL: string; const AParams: string; const AMethod: string; const AHeaders: TNetHeaders): IHTTPResponse;
var
  LHTTPClient: TNetHTTPClient;
  LHTTPRequest: TNetHTTPRequest;
  LParams: TStringStream;
begin
  Result := nil;
  CreateHTTPComponents(LHTTPClient, LHTTPRequest);
  try
    if AMethod = POST then
      LParams := TStringStream.Create(AParams, TEncoding.UTF8)
    else
      LParams := nil;

    try
      Result := ExecuteHTTPRequest(LHTTPRequest, AURL, LParams, AMethod, AHeaders);
    finally
      if Assigned(LParams) then
        LParams.Free;
    end;
  finally
    LHTTPRequest.Free;
    LHTTPClient.Free;
  end;
end;





function TMicrosoftSSO.ParseJsonErrorAccessToken(AResponse: string): string;
var
  LJSONObj: TJSONObject;
  LJSONValue: TJSONValue;
begin
  LJSONValue := TJSONObject.ParseJSONValue(AResponse);
  if  not Assigned(LJSONValue) then
  begin
    Result := JSON_PARSE_ERROR;
    exit;
  end;


  try
     LJSONObj := LJSONValue as TJSONObject;

     Result := LJSONObj.GetValue<string>(ERROR) + ' ' + LJSONObj.GetValue<string>(ERROR_DESCRIPTION);

  finally
      LJSONValue.Free;
  end;



end;

procedure TMicrosoftSSO.ParseJsonErrorUserData(AResponse: string);
var
  LJSONObj: TJSONObject;
begin
  LJSONObj := TJSONObject.ParseJSONValue(AResponse) as TJSONObject;
  try
    if  not Assigned(LJSONObj) then
      raise Exception.Create(JSON_PARSE_ERROR);

    LJSONObj := LJSONObj.GetValue<TJSONObject>(ERROR);
    raise Exception.Create(LJSONObj.GetValue<string>(CODE) + ' ' + LJSONObj.GetValue<string>(MESSAGE));

  finally
    LJSONObj.Free;
  end;
end;

function TMicrosoftSSO.GetAccessToken: string;
var
  LResponse: IHTTPResponse;
begin
  Result := EmptyStr;

  LResponse := ResponseContentAccessToken;
  if not Assigned(LResponse) then
    raise Exception.Create(HTTP_RESPONSE_ERROR);
  //gestione errori
  if LResponse.StatusCode <> HTTP_STATUS_OK then
    raise Exception.Create(ParseJsonErrorAccessToken(LResponse.ContentAsString(GetEncoding)));


  FAccessToken := ParseJsonResponseAccessToken(LResponse.ContentAsString(GetEncoding));

  Result := FAccessToken;
end;

function TMicrosoftSSO.GetUserData: TSSOUserInformationModel;
var
  LResponse: IHTTPResponse;
begin
  Result := nil;

  LResponse := ResponseContentUserData;
  if not Assigned(LResponse) then
    raise Exception.Create(HTTP_RESPONSE_ERROR);
  //gestione errori
  if LResponse.StatusCode <> HTTP_STATUS_OK then
    ParseJsonErrorUserData(LResponse.ContentAsString(GetEncoding));

  Result := ParseJsonResponseUserData(LResponse.ContentAsString(GetEncoding));
end;

procedure TMicrosoftSSO.Logout;
var
  LogoutURL: string;
begin
  if not LoggedIn then
    raise Exception.Create(NOT_LOGGED);

  //invalidate the auth code and access token
  FAuthorizationCode := EmptyStr;
  FAccessToken := EmptyStr;

  // Redirect to Microsoft's logout URL
  LogoutURL := DEFAULT_MICROSOFTURL + LOGOUT_URL + TNetEncoding.URL.Encode(FRedirectURI);
  ShellExecute(0, OPEN, PChar(LogoutURL), nil, nil, SW_SHOWNORMAL);

  LoggedIn := false;
end;

function TMicrosoftSSO.LogIn: TSSOUserInformationModel;
begin
  if LoggedIn then
    raise Exception.Create(ALREADY_LOGGED);
  //Attivo il server e mando la richiesta per il login
  //che ritornerà anche il codice di autorizzazione
  //StartServer;
  //apro la pagina di autorizzazione
  //RedirectToAuthorizationEndpoint;
  //aspetto che il codice di autorizzazione venga ritornato
//  while FAuthorizationCode = EmptyStr do
//    sleep(DEFAULT_SLEEP_TIME);
  //Prendo il token di accesso
  GetAccessToken;
  //Ritorno i dati utente prendendoli tramite access token
  Result := GetUserData;

  LoggedIn := true;
end;


end.

