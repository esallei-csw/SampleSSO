unit LoginSSO.Logic;

interface

uses
  System.SysUtils, System.Classes, IdContext, IdHTTPServer,
  System.Net.HttpClient, System.Net.URLClient, System.Net.HttpClientComponent
 , LoginSSO.User.Information, System.JSON, LoginSSO.Utils, LoginSSO.ServerLogic, LoginSSO.HTTPRequests;

type
  TGoToAccessURL = procedure(AUrl: String) of Object;

  TMicrosoftSSO = class
  private
    { Private declarations }
    FClientID: string;
    FTenantID: string;
    FRedirectURI: string;
    FAuthorizationCode: string;
    FAccessToken: string;
    FLoggedIn: boolean;
    FAuthURL: string;
    FUtils : TSSOUtils;
    FGoToAccessURL: TGoToAccessURL;
    FSSOServer: TSSOServer;
    FSSOHTTPRequests: TSSOHTTPRequests;


    function GetUtils: TSSOUtils;
    function GetSSOServer: TSSOServer;
    function GetSSOHTTPReq: TSSOHTTPRequests;
    procedure InvalidateCodeAndToken;
    function GetLogoutURL: string;

    property Utils: TSSOUtils read GetUtils write FUtils;
    property SSOServer: TSSOServer read GetSSOServer write FSSOServer;
    property SSOHTTPRequests: TSSOHTTPRequests read GetSSOHTTPReq write FSSOHTTPRequests;



  public
    { Public declarations }
    constructor Create(AClientID, ATenantID: string);
    destructor Destroy; override;
    function GetAccessToken: string;
    function GetAuthCode: string;
    procedure LogOut;
    function GetUserData: TSSOUserInformationModel;
    function GetAuthURL: string;
    procedure OpenBrowser(AURL: string);

    function LogIn: TSSOUserInformationModel;

    function GetRedirectURI: string;

    property LoggedIn : boolean read FLoggedIn write FLoggedIn;
    property GoToAccessURL: TGoToAccessURL read FGoToAccessURL write FGoToAccessURL;
  end;

implementation

uses
 System.NetEncoding, Winapi.Windows
 , System.IOUtils, LoginSSO.Constants, LoginSSO.Messages, System.Generics.Collections, IdGlobal, IdSocketHandle, IdException;

 { TMicrosoftSSO }

constructor TMicrosoftSSO.Create(AClientID, ATenantID: string);
begin
  FGoToAccessURL := nil;

  FClientID := AClientID;
  FTenantID := ATenantID;

  //creo e avvio il server
  SSOServer := TSSOServer.create;
  FRedirectURI := SSOServer.RedirectURI;

  FAuthURL := Format(
    DEFAULT_MICROSOFTURL + AUTH_URL,
    [FTenantID, FClientID, TNetEncoding.URL.Encode(FRedirectURI)]);

  LoggedIn := false;

end;

destructor TMicrosoftSSO.Destroy;
begin
  if Assigned(FUtils) then
    FUtils.Free;
  if Assigned(FSSOServer) then
    FSSOServer.Free;
  if Assigned(FSSOHTTPRequests) then
    FSSOHTTPRequests.Free;
  inherited;
end;

function TMicrosoftSSO.GetAuthCode: string;
begin
  Result := SSOServer.AuthCode;
end;

function TMicrosoftSSO.GetAuthURL: string;
begin
  Result := FAuthURL;
end;

function TMicrosoftSSO.GetLogoutURL: string;
begin
  Result := Format(DEFAULT_MICROSOFTURL + FTenantID + LOGOUT_URL + POST_REDIRECT_URL, [FRedirectURI]);
end;

function TMicrosoftSSO.GetRedirectURI: string;
begin
  Result := FRedirectURI;
end;

function TMicrosoftSSO.GetSSOHTTPReq: TSSOHTTPRequests;
begin
  if not Assigned(FSSOHTTPRequests) then
    FSSOHTTPRequests := TSSOHTTPRequests.Create(FClientID, FTenantID);
  Result := FSSOHTTPRequests;
end;

function TMicrosoftSSO.GetSSOServer: TSSOServer;
begin
  if not Assigned(FSSOServer) then
    FSSOServer := TSSOServer.Create;
  Result := FSSOServer;
end;

procedure TMicrosoftSSO.InvalidateCodeAndToken;
begin
  FAuthorizationCode := EmptyStr;
  FAccessToken := EmptyStr;
end;

procedure TMicrosoftSSO.OpenBrowser(AURL: string);
begin
  if not Assigned(FGoToAccessURL) then
    raise Exception.Create(METHOD_NOT_IMPLEMENTED);

  FGoToAccessURL(AURL);
end;

function TMicrosoftSSO.GetAccessToken: string;
var
  LResponse: IHTTPResponse;
begin
  Result := EmptyStr;

  LResponse := SSOHTTPRequests.ResponseContentAccessToken(SSOServer.AuthCode, FRedirectURI);
  if not Assigned(LResponse) then
    raise Exception.Create(HTTP_RESPONSE_ERROR);
  //gestione errori
  if LResponse.StatusCode <> HTTP_STATUS_OK then
    raise Exception.Create(SSOHTTPRequests.ParseJsonErrorAccessToken(LResponse.ContentAsString(Utils.GetEncoding)));

  FAccessToken := SSOHTTPRequests.ParseJsonResponseAccessToken(LResponse.ContentAsString(Utils.GetEncoding));

  Result := FAccessToken;
end;

function TMicrosoftSSO.GetUserData: TSSOUserInformationModel;
var
  LResponse: IHTTPResponse;
begin
  Result := nil;

  LResponse := SSOHTTPRequests.ResponseContentUserData(FAccessToken);
  if not Assigned(LResponse) then
    raise Exception.Create(HTTP_RESPONSE_ERROR);
  //gestione errori
  if LResponse.StatusCode <> HTTP_STATUS_OK then
    raise Exception.Create(SSOHTTPRequests.ParseJsonErrorUserData(LResponse.ContentAsString(Utils.GetEncoding)));

  Result := SSOHTTPRequests.ParseJsonResponseUserData(LResponse.ContentAsString(Utils.GetEncoding));

end;

function TMicrosoftSSO.GetUtils: TSSOUtils;
begin
  if not Assigned(FUtils) then
    FUtils := TSSOUtils.Create;
  Result := FUtils;
end;

procedure TMicrosoftSSO.Logout;
begin
  if not LoggedIn then
    raise Exception.Create(NOT_LOGGED);

  // Redirect to Microsoft's logout URL
  OpenBrowser(GetLogoutURL);

  //invalidate the auth code and access token
  InvalidateCodeAndToken;

  LoggedIn := False;
end;

function TMicrosoftSSO.LogIn: TSSOUserInformationModel;
begin
  if LoggedIn then
    raise Exception.Create(ALREADY_LOGGED);
  //apro la pagina di autorizzazione
  OpenBrowser(FAuthURL);
  //wait a little bit for the auth code to be received
  sleep(SMALL_SLEEP_TIME);
  //if WebModal is closed by user before authentication, code is not received so exception is raised
  if ( SSOServer.AuthCode = EmptyStr ) then
    raise Exception.Create(LOGIN_CANCEL);
  GetAccessToken;
  //Ritorno i dati utente prendendoli tramite access token
  Result := GetUserData;

  LoggedIn := true;
end;

end.
